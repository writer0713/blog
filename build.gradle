buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "com.eriwen.gradle.js" version "2.14.1"
    id "com.eriwen.gradle.css" version "2.14.0"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'js'
apply plugin: 'css'


group = 'com.writer0713'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
//    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.3'
//    compile group: 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:1.1.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

def jsDir = "${projectDir}/src/main/resources/static/js/"
def jsFiles = ["jquery.min.js", "bootstrap.js", "codemirror.min.js", "clike.min.js", "custom.js"]

def cssDir = "${projectDir}/src/main/resources/static/css/"
def cssFiles = ["404.css", "bootstrap.css", "bootstrap-theme.css", "codemirror.min.css", "material.min.css", "style.css"]


combineJs {
    encoding = "UTF-8"
    source = jsFiles.collect { elem -> return jsDir + elem }
    dest = file("${jsDir}/prod/all.js")
}

combineCss {
    source = cssFiles.collect { elem -> return cssDir + elem }
    dest = file("${cssDir}/prod/all.css")
}

minifyJs {
    source = combineJs
    dest = file("${jsDir}/prod/all-min.js")
    sourceMap = file("${jsDir}/prod/all.sourcemap.json")
    closure {
        warningLevel = 'QUIET'
        compilerOptions.languageIn="ECMASCRIPT6"
        compilerOptions.languageOut="ECMASCRIPT5"
    }
}

minifyCss {
    source = combineCss
    dest = "${cssDir}/prod/all-min.css"
    yuicompressor { // Optional
        lineBreakPos = -1
    }
}

assemble.dependsOn(minifyJs, minifyCss)